#!/usr/bin/env perl6
# --------------------
#  This is Perl(6!)
# You have been warned
# --------------------

use v6.d;

# would be (maybe) better to use XML protocol, but I cannot be arsed.
constant $coqtop = 'coqtop';

# coqtop is awful.  We add «About $marker» to everything we say to Coq, then $prompt
# is our clue to stop waiting Coq.
constant $marker = "quiqui_{ ('aa'..'zz').pick }";
constant $prompt = "Expands to: Constant Top.$marker";

constant $type-lines-threshold = 5;
constant $default-arity        = '1-3';

sub succ ($coq --> Str) {
  gather loop {
    with $coq.receive { next unless .so;
                        next if / ^ $marker /;
                        last if / $prompt /;
                        .take;
                      }
  } andthen .join("\n")
}

sub btw ($s) { say "$s.".fmt: '(* %-30s *)' }

sub MAIN (Str $query,
          Str :i(:$in),           #= modules to search
          Str :a(:$arity)         #= N or N-M
         )
{
  my Proc::Async $proc .= new: :w, $coqtop;
  my $chan = $proc.stdout.lines.Channel;
  $proc.stderr.tap: -> $ { };

  class coq {
    method say ($s) { $proc.say: "$s. About $marker." }
    method succ ()  { succ($chan) }
  }

  $proc.start; fail unless $chan.receive ~~ /^ "Welcome to Coq"/;

  btw <HI>;
  coq.say: "Definition $marker := tt";
  coq.succ;

  btw <Loading>;
  coq.say: "From mathcomp Require Import ssrnat ssrbool ssrfun div";
  coq.succ;

  btw "Searching ({ $in || <everything> })";
  coq.say: qq｢Search _ { "in $in" if $in }｣;

  my $r = coq.succ;
  my @defs = $r.split(/<(\n)>\S/).map(* ~~ / <( <.ident>+ % '.' )> ':' /).grep(so*);

  my Str $explicit-arity = $arity;
  without $arity { $explicit-arity = $default-arity unless $query ~~ /forall/ }

  my Int @fn = +.[0] … .[1] with $explicit-arity.split('-');

  my @foralls = False;
  @foralls = gather { (.take for .permutations) for ([\,] ('a'..*)).[--«@fn] } if $explicit-arity;

  btw "{+@defs} entries. {@defs×@foralls} to search" andthen print "\n";

  my Bool %found will keep { btw ("{+$_} found. " if $_) ~ <THXBYE> }

  for @foralls.sort X @defs -> ($args, $def) {
    next if %found{$def};

    my $type = qq｢{"forall $args," if $args} $query｣;
    coq.say: "Check $def : $type";

    if my $checked = coq.succ {
      %found{$def}++;

      coq.say: "Check $def";
      say coq.succ;

      say do with $checked.lines {
        when * > $type-lines-threshold { .[0 ..^ $type-lines-threshold], "\n  ⋮" }
        when 2  { .[1] }
        default {  $_  }
      } andthen print "\n\n";
    }
  }

  <woah>
}
